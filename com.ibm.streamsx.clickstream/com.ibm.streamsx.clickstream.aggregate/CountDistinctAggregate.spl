namespace com.ibm.streamsx.clickstream.aggregate ;

use com.ibm.streamsx.clickstream.util::* ;

public composite CountDistinctAggregate(output stream<CountDistinctWithListAggregate_T> CountDistinctAggregate ; input InputStream)
{
	param
		expression<int64> $WindowInSec ; // Tumbling window in sec
		expression $timestampField ; // Timestamp field - e.g. epoch time in sec 
		expression $wcTimestampField ; // Wall clock timestamp field - e.g. epoch time in sec 
		expression $groupByField ; // Field name to be used for groupBy
		expression $itemField ; // Filed name of items to be counted by distinct
		expression<rstring> $label; // name of the aggregate 
		
	graph
		stream<InputStream> ClkStrmPunctuated = ClickstreamsTimestampPunctor(InputStream)
		{
			param
				WindowInSec :  $WindowInSec ;
				timestampField : $timestampField  ;
		}
		
		stream<CountDistinctWithListAggregate_T> CountDistinctAggregate = Aggregate(ClkStrmPunctuated as T)




		
        {
            window
                T : tumbling, punct();
            param
                groupBy : $groupByField ;
            output
                CountDistinctAggregate : 
                	label = $label,                                                 // rstring label,                              
                	groupName = Any((rstring) $groupByField),                       // rstring groupName,                          
                	itemCollectDistinct = CollectDistinct((rstring) $itemField),    // list<rstring> itemCollectDistinct,          
                    itemCountByDistinct = CountByDistinct($itemField),              // list<int32> itemCountByDistinct,            
                    itemCountDistinct = CountDistinct($itemField),                  // int32 itemCountDistinct,                    
                    itemCount = Count(),                                            // int32 itemCount,                            
                    firstTimestampInSec = First($timestampField),                   // int64 firstTimestampInSec,                  
                    timestampInSec = Last($timestampField),                         // int64 timestampInSec, // lastTimeStamp      
                    wcTimestampFirst = First($wcTimestampField),                    // int64 wcTimestampFirst,                     
                    wcTimestampLast  = Last($wcTimestampField);                     // int64 wcTimestampLast;                      
                                                                                                                                
                    
                              
                              
                              
                              
                              
                              
                              
                              
                              
                              
        } 
}
